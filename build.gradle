plugins {
	id 'maven-publish'
	alias(libs.plugins.quilt.loom)
	alias(libs.plugins.quilt.licenser)
}

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

repositories {
	maven {
		name 'ParchmentMC'
		url 'https://maven.parchmentmc.org'
		content {
			includeModule 'org.parchmentmc.data', 'parchment-1.18.2'
		}
	}
	maven {
		name 'Modrinth'
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		name 'Nucleoid'
		url 'https://maven.nucleoid.xyz'
		content {
			includeGroup 'eu.pb4'
			includeGroup 'xyz.nucleoid'
		}
	}
	mavenLocal() {
		description 'JitPack is unreliable, so these modules have to be cloned and published locally.'
		content {
			includeModule 'org.samo_lego', 'config2brigadier-fabric'
		}
	}
}

// All the dependencies are declared at gradle/libs.version.toml and referenced with "libs.<id>"
// See https://docs.gradle.org/current/userguide/platforms.html for information on how version catalogs work.
dependencies {

//region Minecraft / Quilt
	minecraft libs.minecraft
	mappings loom.layered {
		officialMojangMappings()
		// Use Mojang mappings as your primary mappings, falling back on QM for paras & docs
		parchment("org.parchmentmc.data:parchment-1.18.2:2022.11.06@zip")
	}
	modImplementation libs.quilt.loader

	// QSL is not a complete API; You will need Quilted Fabric API to fill in the gaps.
	// Quilted Fabric API will automatically pull in the correct QSL version.
	modImplementation libs.quilted.fabric.api
//endregion
//region Optimization
	// These mods optimize...
	modRuntimeOnly "maven.modrinth:lazydfu:0.1.2" // Startup
	modRuntimeOnly "maven.modrinth:sodium:mc1.18.2-0.4.1" // Chunk Rendering
	runtimeOnly "org.joml:joml:1.10.5" // Replaced by Mojang's internal math library, but still used by Sodium.
	modRuntimeOnly "maven.modrinth:entityculling:1.5.1-fabric-1.18" // Entity Rendering
	modRuntimeOnly "maven.modrinth:starlight:1.0.2+1.18.2" // Lighting
	modRuntimeOnly "maven.modrinth:ferrite-core:4.2.1-fabric" // Memory

	// Makes Sodium work with Indigo mods
	modRuntimeOnly "maven.modrinth:indium:1.0.7+mc1.18.2"
//endregion
//region Patbox
	// Allows registration of server-only blocks/items
	modImplementation "eu.pb4:polymer:${project.polymer_version}"
	modImplementation "eu.pb4:polymer-blocks:${project.polymer_blocks_version}"
	modImplementation "eu.pb4:sgui:${project.sgui_version}"
//endregion

	modImplementation include("org.samo_lego:config2brigadier-fabric:${project.config2brigadier_version}")

	implementation include("blue.endless:jankson:1.2.1")
}

processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
	withJavadocJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

license {
	rule file("LICENSE")
	include '**/*.java'
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
	}
}
